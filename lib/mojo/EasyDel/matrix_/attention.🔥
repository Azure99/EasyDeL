from .matrix_struct import Matrix
from runtime.llcl import Runtime
from math import math
from algorithm.functional import vectorize


fn rms_norm[
    T: DType, nelts: Int
](
    inout output: DTypePointer[T],
    x: DTypePointer[T],
    w: DTypePointer[T],
    epsilon: SIMD[T, 1],
    size: Int,
    runtime: Runtime,
):
    var loop_val: SIMD[T, nelts] = SIMD[T, nelts](0.0)

    @parameter
    fn _sum_power_operation[_nelts: Int](i: Int):
        if _nelts < nelts:
            loop_val[0] += (x.offset(i).simd_load[_nelts](0) ** 2).reduce_add()
        else:
            loop_val += x.offset(i).simd_load[nelts](0) ** 2

    vectorize[nelts, _sum_power_operation](size)

    var normed: SIMD[T, 1] = loop_val.reduce_add()
    normed = normed / size + epsilon
    normed = 1.0 / math.sqrt(normed)

    @parameter
    fn _norm[_nelts: Int](idx: Int):
        let val = w.simd_load[_nelts](idx) * normed * x.simd_load[_nelts](idx)
        output.offset(idx).simd_store[_nelts](0, val)

    vectorize[nelts, _norm](size)
